// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

task customFindbugs(type: FindBugs) {
    ignoreFailures = true
    effort = "max"
    reportLevel = "low"
    excludeFilter = new File("${project.rootDir}/config/quality/findbugs/findbugs-filter.xml")
    classes = files("${project.rootDir}/app/build/intermediates/classes")

    source = fileTree('app/src/main/java')
    classpath = files()

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "${project.rootDir}/app/build/outputs/findbugs/findbugs.xml"
        }
        html {
            destination "${project.rootDir}/app/build/outputs/findbugs/findbugs.html"
        }
    }
}

task customPmd(type: Pmd) {
    ruleSetFiles = files("${project.rootDir}/config/quality/pmd/pmd-ruleset.xml")
    ignoreFailures = true
    ruleSets = []

    source = fileTree('app/src/main/java')

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "${project.rootDir}/app/build/outputs/pmd/pmd-output.xml"
        }
        html {
            destination "${project.rootDir}/app/build/outputs/pmd/pmd-output.html"
        }
    }
}

task customCheckstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/quality/checkstyle/checkstyle.xml") // Where my checkstyle config is...
    //configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/quality/checkstyle/suppressions.xml").absolutePath // Where is my suppressions file for checkstyle is...

    source = fileTree('app/src/main/java')
    classpath = files()

    reports {
        xml.destination "${project.rootDir}/app/build/outputs/checkstyle/checkstyle-output.xml"
    }

}
